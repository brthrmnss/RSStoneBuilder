<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="400" height="300">
	
	<fx:Metadata>
		[Event(name="recordingEnd", type="events.SoundClipEvent")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import events.SoundClipEvent;
			
			protected var microphone:Microphone;
			protected var soundClip:ByteArray;
			protected var sound:Sound;
			protected var channel:SoundChannel;
			
			public function startRecording():void
			{
				soundClip = new ByteArray();
				microphone = Microphone.getMicrophone();
				microphone.rate = 44;
				microphone.gain = 100;
				microphone.addEventListener(SampleDataEvent.SAMPLE_DATA, microphone_sampleDataHandler);
			}
			
			public function stopRecording():void
			{
				microphone.removeEventListener(SampleDataEvent.SAMPLE_DATA, microphone_sampleDataHandler);
				dispatchEvent(new SoundClipEvent(SoundClipEvent.RECORDING_END, soundClip));
			}
			
			protected function microphone_sampleDataHandler(event:SampleDataEvent):void
			{
				level.width = microphone.activityLevel * 3;
				level.height = microphone.activityLevel * 3;
				
				while (event.data.bytesAvailable)
				{
					var sample:Number = event.data.readFloat();
					soundClip.writeFloat(sample);
				}
			}
			
		]]>
	</fx:Script>

	<s:Label text="Recording Message..." top="20" horizontalCenter="0"/>
	
	<s:Ellipse id="level" horizontalCenter="0" verticalCenter="0">
		<s:fill>
			<s:SolidColor color="#FF0000"/>
		</s:fill>
	</s:Ellipse>
	
	<s:Button label="Stop" click="stopRecording()" height="70" left="0" bottom="0" right="0"/>		

</s:Group>
